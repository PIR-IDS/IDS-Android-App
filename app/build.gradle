plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.devtools.ksp'
    id 'org.jetbrains.dokka'
}

def signedReleaseAvailable = false
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()

if(keystorePropertiesFile.exists() && keystorePropertiesFile.isFile() && keystorePropertiesFile.canRead()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
    signedReleaseAvailable = true
}

android {
    namespace 'fr.pirids.idsapp'

    packagingOptions {
        resources {
            excludes += ['mozilla/public-suffix-list.txt', 'META-INF/DEPENDENCIES', '/META-INF/{AL2.0,LGPL2.1}']
        }
    }

    compileSdk 33

    defaultConfig {
        applicationId "fr.pirids.idsapp"
        minSdk 29
        targetSdk 33
        versionCode 8
        versionName "1.0.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables {
            useSupportLibrary true
        }
    }

    if(signedReleaseAvailable) {
        signingConfigs {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }

    buildTypes {
        debug {
            versionNameSuffix "-DEBUG"
            minifyEnabled false
            shrinkResources false
            debuggable true
        }
        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if(signedReleaseAvailable) {
                signingConfig signingConfigs.release
            }
        }
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.3.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
        freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
    }

    ksp {
        arg('room.schemaLocation', "$projectDir/schemas")
    }
}

dependencies {
    implementation "androidx.core:core-ktx:1.8.0"

    // ------- UI -------

    implementation "androidx.compose.ui:ui:1.2.1"
    implementation "androidx.compose.ui:ui-tooling-preview:1.2.1"
    debugImplementation "androidx.compose.ui:ui-tooling:1.2.1"

    implementation "androidx.compose.material3:material3:1.0.0-alpha16"
    implementation "androidx.compose.material:material-icons-extended:1.2.1"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.5.1"
    implementation "androidx.activity:activity-compose:1.5.1"

    implementation "com.google.accompanist:accompanist-pager:$accompanist_version"
    implementation "com.google.accompanist:accompanist-pager-indicators:$accompanist_version"
    implementation "com.google.accompanist:accompanist-flowlayout:$accompanist_version"
    implementation "com.google.accompanist:accompanist-systemuicontroller:$accompanist_version"

    // ------- NAVIGATION -------

    implementation "androidx.navigation:navigation-compose:2.5.1"
    implementation "com.google.accompanist:accompanist-navigation-animation:$accompanist_version"

    // ------- PERMISSIONS -------

    implementation "com.google.accompanist:accompanist-permissions:$accompanist_version"

    // ------- NETWORK -------

    implementation "org.jsoup:jsoup:1.15.2"

    // ------- SECURITY -------

    implementation "androidx.security:security-crypto:1.1.0-alpha03"
    implementation "io.github.osipxd:encrypted-datastore:1.0.0-alpha02"

    // ------- DATABASE -------

    implementation "androidx.datastore:datastore-preferences:1.0.0"
    implementation "net.zetetic:android-database-sqlcipher:4.5.2"
    implementation "androidx.room:room-runtime:$room_version"
    ksp "androidx.room:room-compiler:$room_version"

    // ------- BACKGROUND -------

    implementation "androidx.work:work-runtime-ktx:2.7.1"

    // ------- TESTS -------

    androidTestImplementation "androidx.compose.ui:ui-test-junit4:1.2.1"
    debugImplementation "androidx.compose.ui:ui-test-manifest:1.2.1"

    testImplementation "org.junit.jupiter:junit-jupiter:5.9.0"
    testImplementation "androidx.room:room-testing:$room_version"

    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"
}